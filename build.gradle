buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
        //google()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
    }
}


apply plugin: 'net.minecraftforge.gradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'java'
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
    google()
    maven { url = 'https://repo.chrisnaude.com' }
    maven { url = 'https://repo.mikeprimm.com' }
    maven { url = 'https://files.minecraftforge.net/maven' }
    //jcenter()
    //maven { url = 'https://oss.sonatype.org/content/repositories/snapshots/' }
}

def jenkinsBuild = System.getenv("BUILD_NUMBER") ?: "SNAPSHOT"
version = "${minecraft_version}-${purpleirc_version}-${jenkinsBuild}"
group = "com.cnaude.purpleirc"
archivesBaseName = "purpleirc"

configurations {
    shade
    compile.extendsFrom shade
}

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

minecraft {
    mappings channel: "${mappings_channel}", version: "${mappings_version}"

    runs {
        client {
           workingDirectory project.file('run')
            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
        }

        server {
            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
        }
    }
}

dependencies {
    //minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2854'

    shade 'com.cnaude.pircbotx:pircbotx:2.1.8-shaded'
    compile 'us.dynmap:DynmapCoreAPI:3.0-SNAPSHOT'
    //shade fileTree (dir: 'dist-lib', include: '*.jar')
    shade 'org.yaml:snakeyaml:1.17'
    shade('com.googlecode.json-simple:json-simple:1.1.1') {
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
    }
}

jar {
    configurations.shade.each { dep ->
        from(project.zipTree(dep)){
            exclude 'META-INF', 'META-INF/**', 'LICENSE.txt'
        }
    }
    manifest {
        attributes([
                "Specification-Title": "purpleirc",
                "Specification-Vendor": "cnaude",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": "purpleirc",
                "Implementation-Version": "${version}",
                "Implementation-Vendor" :"cnaude",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}
